import eei::*;
import corectrl::*;

module core (
    clk   : input   clock                          ,
    rst   : input   reset                          ,
    membus: modport membus_if::<ILEN, XLEN>::master,
) {
    // if = instruction fetch //

    struct if_fifo_type {
        addr: Addr,
        bits: Inst,
    }

    var if_pc          : Addr ; // Program counter.
    var if_is_requested: logic; // Whether fetching.
    var if_pc_requested: Addr ; // Requested address.

    var if_fifo_wready: logic       ;
    var if_fifo_wvalid: logic       ;
    var if_fifo_wdata : if_fifo_type;
    var if_fifo_rready: logic       ;
    var if_fifo_rvalid: logic       ;
    var if_fifo_rdata : if_fifo_type;

    let if_pc_next: Addr = if_pc + 4; // Next program counter.

    var regfile : UIntX<32>;
    let rs1_addr: logic<5>  = inst_bits[19:15];
    let rs2_addr: logic<5>  = inst_bits[24:20];

    let rs1_data: UIntX = if rs1_addr == 0 { 0 } else { regfile[rs1_addr] };
    let rs2_data: UIntX = if rs2_addr == 0 { 0 } else { regfile[rs2_addr] };

    inst if_fifo: fifo #(
        DATA_TYPE: if_fifo_type,
        WIDTH    : 3           ,
    ) (
        clk                   ,
        rst                   ,
        wready: if_fifo_wready,
        wvalid: if_fifo_wvalid,
        wdata : if_fifo_wdata ,
        rready: if_fifo_rready,
        rvalid: if_fifo_rvalid,
        rdata : if_fifo_rdata ,
    );

    always_comb {
        membus.valid = if_fifo.wready_two;
        membus.addr  = if_pc;
        membus.wen   = 0;
        membus.wdata = 'x; // Not write to memory.

        if_fifo_rready = 1;
    }

    always_ff {
        if_reset {
            // Initialize regfile.
            for i: i32 in 0..32 {
                regfile[i] = i;
            }
        }
    }

    always_ff {
        if_reset {
            if_pc           = 0;
            if_is_requested = 0;
            if_pc_requested = 0;
        } else {
            if if_is_requested {
                if membus.rvalid {
                    if_is_requested = membus.ready && membus.valid;
                    if membus.ready && membus.valid {
                        if_pc           = if_pc_next;
                        if_pc_requested = if_pc;
                    }
                }
            } else {
                if membus.ready && membus.valid {
                    if_is_requested = 1;
                    if_pc           = if_pc_next;
                    if_pc_requested = if_pc;
                }
            }
        }

        // Control FIFO.
        if if_is_requested && membus.rvalid {
            if_fifo_wvalid     = 1;
            if_fifo_wdata.addr = if_pc_requested;
            if_fifo_wdata.bits = membus.rdata;
        } else {
            if if_fifo_wvalid && if_fifo_wready {
                if_fifo_wvalid = 0;
            }
        }
    }

    let inst_pc  : Addr     = if_fifo_rdata.addr;
    let inst_bits: Inst     = if_fifo_rdata.bits;
    var inst_ctrl: InstCtrl;
    var inst_imm : UIntX   ;

    inst decoder: inst_decoder (
        bits: inst_bits,
        ctrl: inst_ctrl,
        imm : inst_imm ,
    );

    always_ff {
        if if_fifo_rvalid {
            $display("%h : %h", inst_pc, inst_bits);
            $display("  itype   : %b", inst_ctrl.itype);
            $display("  imm     : %h", inst_imm);
            $display("  rs1[%d] : %h", rs1_addr, rs1_data);
            $display("  rs2[%d] : %h", rs2_addr, rs2_data);
        }
    }
}
